buildscript {
    ext {
        springBootVersion = "1.5.9.RELEASE"
    }
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
        classpath "gradle.plugin.org.detoeuf:swagger-codegen-plugin:1.7.3"
        classpath "io.swagger:swagger-codegen:2.2.3"
    }
}

apply plugin: "org.springframework.boot"
apply plugin: "java"
apply plugin: "idea"
apply plugin: "application"
apply plugin: "org.detoeuf.swagger-codegen"

version "1.0-SNAPSHOT"

sourceCompatibility = 1.8
targetCompatibility = sourceCompatibility

repositories {
    mavenCentral()
}

sourceSets {
    swagger {
        compileClasspath = configurations['swaggerCompile']
        java {
            srcDir file("${project.buildDir.path}/swagger/src/main/java")
        }
    }
    main {
        compileClasspath += swagger.output
        runtimeClasspath += swagger.output
    }
    test {
        compileClasspath += swagger.output
        runtimeClasspath += swagger.output
    }
}

dependencies {
    //Compile
    compile ("org.springframework.boot:spring-boot-starter-web")
    compile ("io.swagger:swagger-annotations:1.5.17")
    compile ("io.swagger:swagger-core:1.5.17")
    swaggerCompile ("io.swagger:swagger-annotations:1.5.17")
    swaggerCompile ("io.swagger:swagger-core:1.5.17")
    swaggerCompile ("io.springfox:springfox-swagger2:2.6.1")
    swaggerCompile ("org.springframework.boot:spring-boot-starter-web:${springBootVersion}")

    //Test
    testCompile ("org.springframework.boot:spring-boot-starter-test")
    testCompile ("junit:junit:4.12")
}

bootRun {
    classpath = sourceSets.main.runtimeClasspath
}

swagger {
    inputSpec = "${project.projectDir}/src/main/resources/api.yml"
    outputDir = file("${project.buildDir.path}/swagger")
    lang = 'spring'

    additionalProperties = [
            'modelPackage'     : 'com.avelvi.ws.model',
            'apiPackage'       : 'com.avelvi.ws.api',
            'interfaceOnly'    : 'true',
            'dateLibrary'      : 'java8',
            'java8'            : 'true',
            'library'          : 'spring-boot'
    ]

}

classes.dependsOn('swaggerClasses')
build.dependsOn('test')